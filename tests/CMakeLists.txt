cmake_minimum_required(VERSION 3.22) # because of PROPERTIES ENVIRONMENT_MODIFICATION

find_package(GTest CONFIG REQUIRED)

add_executable(api_tests api_tests.cpp)
target_link_libraries(api_tests PRIVATE docwire_core GTest::gtest_main)
if(MSVC)
    find_package(PThreads4W REQUIRED)
    target_link_libraries(api_tests PRIVATE PThreads4W::PThreads4W)
endif()

find_package(Boost REQUIRED COMPONENTS json)
target_link_libraries(api_tests PRIVATE Boost::json)
target_include_directories(api_tests PRIVATE ${Boost_INCLUDE_DIRS})

file(GLOB test_files *)
list(FILTER test_files EXCLUDE REGEX ".*\\.cpp$")
file(COPY ${test_files} DESTINATION .)

include(GoogleTest)

if(WIN32)
	set(ENV_VAR_NAME "PATH")
elseif(APPLE)
	set(ENV_VAR_NAME "DYLD_FALLBACK_LIBRARY_PATH")
else()
	set(ENV_VAR_NAME "LD_LIBRARY_PATH")
endif()

set(TEST_ENVIRONMENT_MODIFICATION_PROPERTY "")
foreach(path ${TEST_PREPEND_LIBRARY_PATH})
	file(TO_NATIVE_PATH "${path}" path)
	list(APPEND TEST_ENVIRONMENT_MODIFICATION_PROPERTY "${ENV_VAR_NAME}=path_list_prepend:${path}")
endforeach()
message(STATUS "Test environment modification property: ${TEST_ENVIRONMENT_MODIFICATION_PROPERTY}")

gtest_add_tests(TARGET api_tests TEST_LIST API_TESTS)
set_tests_properties(${API_TESTS} PROPERTIES ENVIRONMENT_MODIFICATION "${TEST_ENVIRONMENT_MODIFICATION_PROPERTY}")

file(READ ../README.md content)
set(n 1)
while (TRUE)
	string(FIND "${content}" "```cpp" start_pos)
	if (start_pos EQUAL -1)
		break()
	endif()
	math(EXPR start_pos "${start_pos} + 6")
	string(SUBSTRING "${content}" ${start_pos} -1 content)
	string(FIND "${content}" "```" end_pos)
	if (end_pos EQUAL -1)
		message(FATAL_ERROR "Failed to find end of code block starting at position ${start_pos}")
	endif()
	string(SUBSTRING "${content}" 0 ${end_pos} code_block)
	set(example_file "${CMAKE_BINARY_DIR}/example_${n}.cpp")
	file(WRITE ${example_file} "${code_block}")
	add_executable(example_${n} ${example_file})
	target_include_directories(example_${n} PUBLIC ../src)
	target_link_libraries(example_${n} PRIVATE docwire_core)
	string(FIND "${code_block}" "OPENAI_API_KEY" openai_api_key_pos)
	if (openai_api_key_pos EQUAL -1 OR NOT "$ENV{OPENAI_API_KEY}" STREQUAL "")
		add_test(NAME example_${n} COMMAND example_${n})
		set_tests_properties(example_${n} PROPERTIES ENVIRONMENT_MODIFICATION "${TEST_ENVIRONMENT_MODIFICATION_PROPERTY}")
	endif()
	math(EXPR n "${n} + 1")
	math(EXPR end_pos "${end_pos} + 3")
	string(SUBSTRING "${content}" ${end_pos} -1 content)
endwhile()
